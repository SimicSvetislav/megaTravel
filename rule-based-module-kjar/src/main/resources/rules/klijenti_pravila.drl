//created on: 14.04.2019.
package rules

//list any import classes here.
import com.project.megatravel.model.users.KrajnjiKorisnik
import com.project.megatravel.model.reservations.RezervacijaKorisnika
import com.project.megatravel.model.users.Kupon


//declare any global variables here
global List kuponi;

// BRONZE
rule "Klijenti bez kategorije"
	agenda-group "klijent"
	lock-on-active true
	salience 10
    when
        $klijent : KrajnjiKorisnik ( kategorija == "NA", $id : id )
    then
    	System.out.println("Klijentu " + $id + " dodeljena kategorija BRONZE");
        modify ($klijent) { setKategorija("BRONZE"); }

end

// SILVER
rule "Klijent registrovan vise od 6 meseci i ukupna cena rezervacija prelazi 300 eura"
    agenda-group "klijent"
    lock-on-active true
    salience 20
    when
        $klijent : KrajnjiKorisnik ( $registracija : datumRegistracije, kategorija != "GOLD", kategorija != "PLATINUM", !datumUOpsegu($registracija, 180), 
        $rezervacije : rezervacije, $id : id )
        Number (doubleValue >= 300 ) from accumulate (
			RezervacijaKorisnika (
				stanje == "REALIZOVANO", 
				$cena : cenaSmestaja
			) from $rezervacije,
			sum($cena)
		)

    then
        System.out.println("Klijentu " + $id + " dodeljena kategorija SILVER (uslov 1)");
        modify ($klijent) { setKategorija("SILVER"); }

end

rule "Klijent ima dve ili vise uspesno realizovane rezervacije i ukupna cena rezervacija prelazi 300 eura"
    agenda-group "klijent"
    lock-on-active true
    salience 20
    when
        $klijent : KrajnjiKorisnik ( $rezervacije : rezervacije, kategorija != "GOLD", kategorija != "PLATINUM", $id : id )
        Number (intValue >= 2 ) from accumulate (
			RezervacijaKorisnika (
				stanje == "REALIZOVANO" 
			) from $rezervacije,
			count(1)
		)
        Number (doubleValue >= 300 ) from accumulate (
			RezervacijaKorisnika (
				stanje == "REALIZOVANO", 
				$cena : cenaSmestaja
			) from $rezervacije,
			sum($cena)
		)
		
    then
        System.out.println("Klijentu " + $id + " dodeljena kategorija SILVER (uslov 2)");
        modify ($klijent) { setKategorija("SILVER"); }

end

// GOLD
rule "Klijent ima deset ili vise uspesno realizovanih rezervacija i klijent je boravio u barem jednom smestaju sa GOLD ili vecom kategorijom"
    agenda-group "klijent"
    lock-on-active true
    salience 30
    when
        $klijent : KrajnjiKorisnik ($rezervacije : rezervacije, kategorija != "PLATINUM", kategorija != "GOLD", $id : id)
        ( Number (intValue >= 1 ) from accumulate (
			RezervacijaKorisnika (
				stanje == "REALIZOVANO",
				$kat : smestaj.getSObjekat().getKategorija(),
				$kat == "GOLD"
			) from $rezervacije,
			count(1)
		) or Number (intValue >= 1 ) from accumulate (
			RezervacijaKorisnika (
				stanje == "REALIZOVANO",
				$kat : smestaj.getSObjekat().getKategorija(),
				$kat == "PLATINUM"
			) from $rezervacije,
			count(1)
		) )
        Number (intValue >= 10 ) from accumulate (
			RezervacijaKorisnika (
				stanje == "REALIZOVANO" 
			) from $rezervacije,
			count(1)
		)
    then
        System.out.println("Klijentu " + $id + " dodeljena kategorija GOLD");
       
        Kupon k = new Kupon();
        k.setTip("GOLD");
        k.setKorisnik($klijent);
        kuponi.add(new Kupon());
        
        modify ($klijent) { setKategorija("GOLD"); }

end

// PLATINUM
rule "Klijent ima rezervacije u poslednjih 12 meseci cija je ukupna cena veca od 2000 eura"
    agenda-group "klijent"
    lock-on-active true
    salience 40
    when
        $klijent : KrajnjiKorisnik ($rezervacije : rezervacije, kategorija != "PLATINUM",  $id : id)
        Number (doubleValue >= 2000 ) from accumulate (
			RezervacijaKorisnika (
				stanje == "REALIZOVANO",
				$datumKraja : datumZavrsetka,
				datumUOpsegu ($datumKraja, 365),
				$cena : cenaSmestaja
			) from $rezervacije,
			sum($cena)
		)
    then
        System.out.println("Klijentu " + $id + " dodeljena kategorija PLATINUM");
        
        Kupon k = new Kupon();
        k.setTip("PLATINUM");
        k.setKorisnik($klijent);
        kuponi.add(new Kupon());
        
        modify ($klijent) { setKategorija("PLATINUM"); }

end

// Halt
rule "Halt klijenti"
	agenda-group "klijent"
	salience -100
    when
        // Izvrsava se bezuslovno posle svih pravila koja pripadaju grupi "klijent"
    then
    	System.out.println("HALT");
        drools.halt();

end
