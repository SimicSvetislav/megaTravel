//created on: 14.04.2019.
package rules

//list any import classes here.

import java.util.Date
import java.util.List

import com.project.megatravel.model.reservations.RezervacijaKorisnika
import com.project.megatravel.model.accomodation.SmestajniObjekat
import com.project.megatravel.model.accomodation.SmestajnaJedinica
import com.project.megatravel.model.accomodation.Lokacija
import com.project.megatravel.events.LastMinuteEvent;
import com.project.megatravel.events.EarlyBirdEvent;

global Boolean finalizing;
global Boolean lmEnabled;
global Boolean ebEnabled;

rule "Last-minute"
	agenda-group "popusti"
	salience 50
	lock-on-active true
    when	
        $rez : RezervacijaKorisnika( $datumRezervacije : datumRezervacije, $datumPocetka : datumPocetka, popust == 0.0,  sameDate($datumRezervacije, $datumPocetka), $id : id  )
    then
    	System.out.println("\nLast-minute\nDodeljen popust 50% za rezervaciju " + $id );
        modify ($rez) { setPopust(50); }
        if (finalizing!=null && finalizing) {
        	Lokacija $lokacija = nadjiLokaciju($rez);
        	System.out.println("Inserted LM event" + $lokacija.getNaziv());
        	insert (new LastMinuteEvent());
        }

end

rule "Early bird"
	agenda-group "popusti"
	salience 25
	lock-on-active true
    when
        $rez : RezervacijaKorisnika( $datumP : datumPocetka , popust == 0.0, earlyBird($datumP), $id : id  )
    then
        System.out.println("\nEarly bird\nDodeljen popust 25% za rezervaciju " + $id );
        modify ($rez) { setPopust(25); }
        if (finalizing!=null && finalizing) {
        	Lokacija $lokacija = nadjiLokaciju($rez);
        	System.out.println("Inserted EB event" + $lokacija.getNaziv());
        	insert (new EarlyBirdEvent());
        }
end

rule "Popust na neocenjene smestaje u istom mestu u kojem su klijenti boravili barem 6 puta kod barem 3 razlicita agenta"
	agenda-group "popusti"
	salience 60
	lock-on-active true
    when
        $rez : RezervacijaKorisnika( stanje == "REZERVISANO", popust == 0.0, $id : id,  $lokId : smestaj.getSObjekat().getLokacija().getId(), $korisnikId : korisnik.getId()  )
        $rezervacije : List() from collect ( RezervacijaKorisnika( stanje == "REALIZOVANO", smestaj.getSObjekat().getLokacija().getId() == $lokId, korisnik.getId() == $korisnikId ) )
        $agenti : Set() from accumulate (
			RezervacijaKorisnika (
				$idA : smestaj.getSObjekat().getAgent().getId()
			) from $rezervacije,
			collectSet($idA)
		)
        $so : SmestajniObjekat( lokacija.getId() == $lokId, $rezervacije.size() >= 6, $agenti.size() >= 3, rejting.getBrojOcena() == 0 )
    then
        System.out.println("\nDodatni popust (1)\nDodeljen popust 60% za rezervaciju " + $id );
        System.out.println($rezervacije.size());
        System.out.println($agenti.size());
        modify ($rez) { setPopust(60); }

end

rule "Popust na neocenjene smestaje na lokaciji u kojoj klijenti nisu prethodno boravili, a koji su u prethodnoj godini posetili 6 ili vise razlicitih mesta"
	agenda-group "popusti2"
	salience 80
	lock-on-active true
    when
        $rez : RezervacijaKorisnika( stanje == "REZERVISANO", popust == 0.0, $id : id,  $lokId : smestaj.getSObjekat().getLokacija().getId(), $korisnikId : korisnik.getId()  )
        $rezervacije : List() from collect ( RezervacijaKorisnika( datumUOpsegu(datumZavrsetka, 365), stanje == "REALIZOVANO", korisnik.getId() == $korisnikId ) )
        $lSet : Set() from accumulate (
			RezervacijaKorisnika (
				$idL : smestaj.getSObjekat().getLokacija()
			) from $rezervacije,
			collectSet($idL)
		)
		$so : SmestajniObjekat( lokacija not memberOf $lSet, $lSet.size() >= 6 )
    then
        System.out.println("\nDodatni popust (1)\nDodeljen popust 80% za rezervaciju " + $id );
        System.out.println($lSet.size());
        modify ($rez) { setPopust(80); }

end

rule "Halt popusti"
	agenda-group "popusti"
	salience -100
    when
        // Izvrsava se bezuslovno posle svih pravila koja pripadaju grupi "popusti"
    then
    	System.out.println("HALT");
        drools.halt();

end

rule "Halt popusti2"
	agenda-group "popusti2"
	salience -100
    when
        // Izvrsava se bezuslovno posle svih pravila koja pripadaju grupi "popusti"
    then
    	System.out.println("HALT");
        drools.halt();

end